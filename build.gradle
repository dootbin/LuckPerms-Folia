plugins {
    id 'java'
    id 'maven-publish'
    id 'org.cadixdev.licenser' version '0.6.1'
}

defaultTasks 'licenseFormat', 'build'

allprojects {
    group = 'me.lucko.luckperms'
    version = '5.4-SNAPSHOT'

    repositories {
        mavenCentral()
        maven { url 'https://s01.oss.sonatype.org/content/repositories/snapshots/' }
        maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }
        maven { url 'https://repo.lucko.me/' }
        maven { url 'https://libraries.minecraft.net/' }
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'maven-publish'
    apply plugin: 'org.cadixdev.licenser'

    java {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }

    tasks.withType(JavaCompile).configureEach {
        options.encoding = 'UTF-8'
    }

    tasks.withType(Test).configureEach {
        testLogging {
            events = [org.gradle.api.tasks.testing.logging.TestLogEvent.PASSED, org.gradle.api.tasks.testing.logging.TestLogEvent.FAILED, org.gradle.api.tasks.testing.logging.TestLogEvent.SKIPPED]
            exceptionFormat = org.gradle.api.tasks.testing.logging.TestExceptionFormat.FULL
            showExceptions = true
            showCauses = true
            showStackTraces = true
        }
    }

    jar {
        from '../LICENSE.txt'
    }

    def determinePatchVersion = {
        def tagInfo = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'describe', '--tags'
            standardOutput = tagInfo
        }
        tagInfo = tagInfo.toString().trim()
        if (!tagInfo.contains('-')) {
            return 0
        }
        return tagInfo.split("-")[1]
    }

    ext {
        majorVersion = '5'
        minorVersion = '4'
        patchVersion = determinePatchVersion()
        apiVersion = "${majorVersion}.${minorVersion}"
        fullVersion = "${apiVersion}.${patchVersion}"
    }

    license {
        header = rootProject.file('HEADER.txt')
        include '**/*.java'
        newLine = true
    }
}